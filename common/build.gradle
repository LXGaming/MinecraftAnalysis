plugins {
    id "java-library"
    id "maven-publish"
    id "net.kyori.blossom"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "minecraftanalysis-common"

blossom {
    replaceToken("@version@", version)
}

configurations {
}

repositories {
}

dependencies {
    api("com.google.code.gson:gson:2.8.6")
    api("io.github.lxgaming:reconstruct-common:1.3.9")
    api("net.fabricmc:tiny-mappings-parser:0.3.+")
    api("net.minecraftforge:srgutils:0.4.1")
    api("org.slf4j:slf4j-api:1.7.30")
    testCompile("org.junit.jupiter:junit-jupiter-engine:5.7.1")
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    failOnError = false
}

processResources {
    from("../LICENSE")
    rename("LICENSE", "LICENSE-MinecraftAnalysis")
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            pom {
                name = "MinecraftAnalysis"
                description = "Minecraft Analysis"
                url = "https://github.com/LXGaming/MinecraftAnalysis"
                developers {
                    developer {
                        id = "lxgaming"
                        name = "LXGaming"
                    }
                }
                issueManagement {
                    system = "GitHub Issues"
                    url = "https://github.com/LXGaming/MinecraftAnalysis/issues"
                }
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    connection = "scm:git@github.com:LXGaming/MinecraftAnalysis.git"
                    developerConnection = "scm:git@github.com:LXGaming/MinecraftAnalysis.git"
                    url = "https://github.com/LXGaming/MinecraftAnalysis"
                }
            }
        }
    }
    repositories {
        if (project.hasProperty("sonatype.username") && project.hasProperty("sonatype.password")) {
            maven {
                name = "sonatype"
                url = version.contains("-SNAPSHOT") ? "https://oss.sonatype.org/content/repositories/snapshots" : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username project.property("sonatype.username")
                    password project.property("sonatype.password")
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

test {
    useJUnitPlatform()
}