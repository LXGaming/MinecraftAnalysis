plugins {
    id "com.jfrog.bintray"
    id "java"
    id "java-library"
    id "maven-publish"
    id "net.kyori.blossom"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "minecraftanalysis-common"

blossom {
    replaceToken("@version@", version)
}

configurations {
}

repositories {
}

dependencies {
    api("com.google.code.gson:gson:2.8.6")
    api("io.github.lxgaming:classloaderutils:1.0.0")
    api("io.github.lxgaming:reconstruct-common:1.3.3") {
        exclude(module: "proguard-base")
    }
    api("org.slf4j:slf4j-api:1.7.30")
    testCompile("org.junit.jupiter:junit-jupiter-engine:5.7.0")
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["mavenPublication"]
    pkg {
        repo = "maven"
        name = "MinecraftAnalysis"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/LXGaming/MinecraftAnalysis.git"
        publish = true
        version {
            name = project.version
            released = new Date()
        }
    }
}

bintrayUpload {
    onlyIf { System.getenv("BINTRAY_KEY") != null }
    onlyIf { System.getenv("BINTRAY_USER") != null }
}

javadoc {
    failOnError = false
}

processResources {
    from("../LICENSE")
    rename("LICENSE", "LICENSE-MinecraftAnalysis")
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

test {
    useJUnitPlatform()
}