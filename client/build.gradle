plugins {
    id "com.github.johnrengelman.shadow"
    id "java"
    id "net.kyori.blossom"
    id "signing"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "io.github.lxgaming"
archivesBaseName = "minecraftanalysis-client"
version = version

blossom {
    replaceToken("@version@", version)
}

configurations {
    shadow {
        runtime.extendsFrom(shadow)
    }

    build.dependsOn("signJar")
    jar.enabled = false
    jar.dependsOn("shadowJar")
}

repositories {
    jcenter()
}

dependencies {
    shadow("com.beust:jcommander:1.78")
    shadow("com.google.code.gson:gson:2.8.6")
    shadow("io.github.lxgaming:classloaderutils:1.0.0")
    annotationProcessor("org.apache.logging.log4j:log4j-core:2.13.3")
    shadow("org.apache.logging.log4j:log4j-core:2.13.3")
    shadow("org.fusesource.jansi:jansi:1.18")
    testCompile("org.junit.jupiter:junit-jupiter-engine:5.6.2")
}

jar {
    manifest {
        attributes("Main-Class": "io.github.lxgaming.minecraftanalysis.client.Main")
    }
}

processResources {
    from("../LICENSE")
    rename("LICENSE", "LICENSE-MinecraftAnalysis")
}

shadowJar {
    archiveClassifier.set(null)
    configurations = [project.configurations.shadow]

    exclude {
        // Exclude the Log4j2Plugins.dat file so logging works. Log4j2PluginsCacheFileTransformer doesn't work.
        if (it.path == "META-INF/org/apache/logging/log4j/core/config/plugins/Log4j2Plugins.dat") {
            return true
        }

        return false
    }
}

test {
    useJUnitPlatform()
}

task signJar {
    doFirst {
        if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
            project.logger.warn("========== [WARNING] ==========")
            project.logger.warn("")
            project.logger.warn("   This build is not signed!   ")
            project.logger.warn("")
            project.logger.warn("========== [WARNING] ==========")
            throw new StopExecutionException()
        }
    }

    doLast {
        configurations.archives.allArtifacts.files.each {
            ant.signjar(
                jar: it,
                alias: project.property("signing.alias"),
                storepass: project.property("signing.keyStorePassword"),
                keystore: project.property("signing.keyStorePath"),
                keypass: project.property("signing.keyStorePassword"),
                preservelastmodified: project.property("signing.preserveLastModified"),
                tsaurl: project.property("signing.timestampAuthority"),
                digestalg: project.property("signing.digestAlgorithm")
            )
            project.logger.lifecycle("JAR Signed: " + it.name)

            signing.sign(it)
            project.logger.lifecycle("PGP Signed: " + it.name)
        }
    }
}